function Entity.TickConveyorItem() {
    // conveys.item_display
    execute if entity @s[tag=conveys.item_display] run {
        execute 
        positioned ITEM_MODEL_OFFSET
        if block ~ ~-0.1 ~ #conveys:belts 
        run {
            Entity.ItemOnActiveConveyor();
        }
        execute positioned ITEM_MODEL_OFFSET unless block ~ ~-0.1 ~ #conveys:belts at @s run Entity.UndirectionalKillConveyorItem();
    }

    // conveys.block_display
    execute if entity @s[tag=conveys.block_display] run {
        execute 
        positioned BLOCK_MODEL_OFFSET
        if block ~ ~-0.1 ~ #conveys:belts 
        run {
            Entity.ItemOnActiveConveyor();
        }
        execute positioned BLOCK_MODEL_OFFSET unless block ~ ~-0.1 ~ #conveys:belts at @s run Entity.UndirectionalKillConveyorItem();
    }
}

function Entity.PositionConveyorItem() {
    tp @s ~ ~-1024 ~;
    tag @s remove conveys.conveyor_item_to_position;
}

function Entity.ItemOnActiveConveyor() {
    execute if entity @s[tag=conveys.item_display] run {
        execute if block ~ ~-0.1 ~ minecraft:white_glazed_terracotta[facing=east] positioned REVERSE_ITEM_MODEL_OFFSET  run {
            Entity.MoveItemItemEast();
        }
        execute if block ~ ~-0.1 ~ minecraft:white_glazed_terracotta[facing=north] positioned REVERSE_ITEM_MODEL_OFFSET run {
            Entity.MoveItemItemNorth();
        }
        execute if block ~ ~-0.1 ~ minecraft:white_glazed_terracotta[facing=south] positioned REVERSE_ITEM_MODEL_OFFSET run {
            Entity.MoveItemItemSouth();
        }
        execute if block ~ ~-0.1 ~ minecraft:white_glazed_terracotta[facing=west] positioned REVERSE_ITEM_MODEL_OFFSET  run {
            Entity.MoveItemItemWest();
        }
    }

    execute if entity @s[tag=conveys.block_display] run {
        execute if block ~ ~-0.1 ~ minecraft:white_glazed_terracotta[facing=east] positioned REVERSE_BLOCK_MODEL_OFFSET  run {
            Entity.MoveBlockItemEast();
        }
        execute if block ~ ~-0.1 ~ minecraft:white_glazed_terracotta[facing=north] positioned REVERSE_BLOCK_MODEL_OFFSET run {
            Entity.MoveBlockItemNorth();
        }
        execute if block ~ ~-0.1 ~ minecraft:white_glazed_terracotta[facing=south] positioned REVERSE_BLOCK_MODEL_OFFSET run {
            Entity.MoveBlockItemSouth();
        }
        execute if block ~ ~-0.1 ~ minecraft:white_glazed_terracotta[facing=west] positioned REVERSE_BLOCK_MODEL_OFFSET  run {
            Entity.MoveBlockItemWest();
        }
    }
    
    execute positioned ~ ~-0.1 ~ align xyz run {
        execute if block ~ ~ ~ minecraft:white_glazed_terracotta[facing=east]  run {
            setblock ~ ~ ~ minecraft:orange_glazed_terracotta[facing=east];
        }
        execute if block ~ ~ ~ minecraft:white_glazed_terracotta[facing=north] run {
           setblock ~ ~ ~ minecraft:orange_glazed_terracotta[facing=north];
        }
        execute if block ~ ~ ~ minecraft:white_glazed_terracotta[facing=south] run {
            setblock ~ ~ ~ minecraft:orange_glazed_terracotta[facing=south];
        }
        execute if block ~ ~ ~ minecraft:white_glazed_terracotta[facing=west]  run {
           setblock ~ ~ ~ minecraft:orange_glazed_terracotta[facing=west];
        }
        summon marker ~ ~ ~ {Tags:["conveys.inactive_conveyor"]}
    }


}

// item display
// 
// yes, we implement this twice to skip a check for a tag on an entity

function Entity.MoveItemItemEast() { 
    execute at @s positioned ~ ~ ~0.755 align z positioned ~0.25 ~ ~-0.255 facing entity @s feet positioned ^ ^ ^0.15 run {
        execute positioned ITEM_MODEL_OFFSET unless block ~ ~-0.1 ~ #conveys:belts at @s positioned ITEM_MODEL_OFFSET run Entity.KillConveyorItem();
        tp @s ~ ~ ~;
    }
}

function Entity.MoveItemItemNorth() { 
    execute at @s positioned ~ ~ ~0.004 align x positioned ~0.496 ~ ~-0.25 facing entity @s feet positioned ^ ^ ^0.15 run {
        execute positioned ITEM_MODEL_OFFSET unless block ~ ~-0.1 ~ #conveys:belts at @s positioned ITEM_MODEL_OFFSET run Entity.KillConveyorItem();
        tp @s ~ ~ ~;
    }
}

function Entity.MoveItemItemSouth() { 
    execute at @s positioned ~ ~ ~0.004 align x positioned ~0.496 ~ ~0.25 facing entity @s feet positioned ^ ^ ^0.15 run {
        execute positioned ITEM_MODEL_OFFSET unless block ~ ~-0.1 ~ #conveys:belts at @s positioned ITEM_MODEL_OFFSET run Entity.KillConveyorItem();
        tp @s ~ ~ ~;
    }
}

function Entity.MoveItemItemWest() { 
    execute at @s positioned ~ ~ ~0.755 align z positioned ~-0.25 ~ ~-0.255 facing entity @s feet positioned ^ ^ ^0.15 run {
        execute positioned ITEM_MODEL_OFFSET unless block ~ ~-0.1 ~ #conveys:belts at @s positioned ITEM_MODEL_OFFSET run Entity.KillConveyorItem();
        tp @s ~ ~ ~;
    }
}

// block display
// 
// yes, we implement this twice to skip a check for a tag on an entity

function Entity.MoveBlockItemEast() { 
    execute at @s positioned ~ ~ ~ align z positioned ~0.25 ~ ~0.5 facing entity @s feet positioned ^ ^ ^0.15 run {
        execute positioned BLOCK_MODEL_OFFSET unless block ~ ~-0.1 ~ #conveys:belts at @s positioned BLOCK_MODEL_OFFSET run Entity.KillConveyorItem();
        tp @s ~ ~ ~;
    }
}

function Entity.MoveBlockItemNorth() { 
    execute at @s positioned ~ ~ ~ align x positioned ~0.5 ~ ~-0.25 facing entity @s feet positioned ^ ^ ^0.15 run {
        execute positioned BLOCK_MODEL_OFFSET unless block ~ ~-0.1 ~ #conveys:belts at @s positioned BLOCK_MODEL_OFFSET run Entity.KillConveyorItem();
        tp @s ~ ~ ~;
    }
}

function Entity.MoveBlockItemSouth() { 
    execute at @s positioned ~ ~ ~ align x positioned ~0.5 ~ ~0.25 facing entity @s feet positioned ^ ^ ^0.15 run {
        execute positioned BLOCK_MODEL_OFFSET unless block ~ ~-0.1 ~ #conveys:belts at @s positioned BLOCK_MODEL_OFFSET run Entity.KillConveyorItem();
        tp @s ~ ~ ~;
    }
}

function Entity.MoveBlockItemWest() { 
    execute at @s positioned ~ ~ ~ align z positioned ~-0.25 ~ ~0.5 facing entity @s feet positioned ^ ^ ^0.15 run {
        execute positioned BLOCK_MODEL_OFFSET unless block ~ ~-0.1 ~ #conveys:belts at @s positioned BLOCK_MODEL_OFFSET run Entity.KillConveyorItem();
        tp @s ~ ~ ~;
    }
}

function Entity.KillConveyorItem() {
    execute if block ~ ~-0.1 ~ minecraft:white_glazed_terracotta[facing=east] run {
        execute align y run summon minecraft:item ~0.15 ~1 ~ {PickupDelay:20,Motion:[0.15,0.1,0.0],Tags:["conveys.subject"],Item:{id:"minecraft:oak_boat",Count:1b}}
    }
    execute if block ~ ~-0.1 ~ minecraft:white_glazed_terracotta[facing=north] run {
        execute align y run summon minecraft:item ~ ~ ~-0.15 {PickupDelay:20,Motion:[0.0,0.1,-0.15],Tags:["conveys.subject"],Item:{id:"minecraft:oak_boat",Count:1b}}
    }
    execute if block ~ ~-0.1 ~ minecraft:white_glazed_terracotta[facing=south] run {
        execute align y run summon minecraft:item ~ ~ ~0.15 {PickupDelay:20,Motion:[0.0,0.1,0.15],Tags:["conveys.subject"],Item:{id:"minecraft:oak_boat",Count:1b}}
    }
    execute if block ~ ~-0.1 ~ minecraft:white_glazed_terracotta[facing=west] run {
        execute align y run summon minecraft:item ~-0.15 ~ ~ {PickupDelay:20,Motion:[-0.15,0.1,0.0],Tags:["conveys.subject"],Item:{id:"minecraft:oak_boat",Count:1b}}
    }
    
    // transfer data to item
    tag @s add conveys.object;
    execute as @e[tag=conveys.subject,limit=1] run {
        data modify entity @s Item set from entity @e[tag=conveys.object,limit=1] ArmorItems[3];
        tag @s remove conveys.subject;
    }
    tag @s remove conveys.object;

    kill @s;
}

function Entity.UndirectionalKillConveyorItem() {
    execute align y run summon minecraft:item ~ ~ ~ {PickupDelay:20,Motion:[0.0,0.0,0.0],Tags:["conveys.subject"],Item:{id:"minecraft:oak_boat",Count:1b}}
    
    // transfer data to item
    tag @s add conveys.object;
    execute as @e[tag=conveys.subject,limit=1] run {
        data modify entity @s Item set from entity @e[tag=conveys.object,limit=1] ArmorItems[3];
        tag @s remove conveys.subject;
    }
    tag @s remove conveys.object;

    kill @s;
}
